plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'checkstyle'
}

group = 'com.glencoesoftware.omero'
version = '0.6.0-SNAPSHOT'

// Java 11 compatibility configuration
java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

// Ensure UTF-8 encoding for compilation
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name 'Unidata'
        url 'https://artifacts.unidata.ucar.edu/repository/unidata-releases/'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/ome.releases'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/ome.external'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/scijava-thirdparty'
    }
    maven {
        url 'https://repo.glencoesoftware.com/repository/bioformats2raw2ometiff/'
    }
}

dependencies {
    api 'org.openmicroscopy:omero-blitz:5.8.3'

    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
    implementation 'dev.zarr:jzarr:0.4.2'

    implementation 'org.carlspring.cloud.aws:s3fs-nio:3.0.0'

    implementation 'software.amazon.awssdk:s3:2.34.6'
    implementation 'org.apache.tika:tika-core:1.28.5'
    implementation 'net.java.dev.jna:jna:5.10.0'
    implementation 'ome:formats-gpl:7.3.1'
    implementation 'info.picocli:picocli:4.7.5'
    implementation 'com.univocity:univocity-parsers:2.8.4'
    implementation 'dev.zarr:zarr-java:0.0.5-SNAPSHOT'
    implementation 'javax.xml.bind:jaxb-api:2.3.0'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.14'
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.3.14'
    implementation 'org.apache.maven:maven-artifact:3.9.4'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.20.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'

    testImplementation 'info.picocli:picocli:4.7.5'
    testImplementation 'com.glencoesoftware:bioformats2raw:0.11.0'
}

test {
  useJUnit()
}

publishing {
    publications {
        omeroZarrPixelBuffer(MavenPublication) {
            from components.java
            pom {
                name = 'OMERO Zarr Pixel Buffer'
                licenses {
                    license {
                        name = 'GNU General Public License version 2'
                        url = 'http://www.gnu.org/licenses/gpl-2.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url 'https://artifacts.glencoesoftware.com/artifactory/gs-omero-snapshots-local/'
            credentials {
               username = project.properties.ArtifactoryUserName
               password = project.properties.ArtifactoryPassword
            }
        }
    }
}

jar {
    manifest {
        attributes(
            "Created-By": "Gradle ${gradle.gradleVersion}",
            "Build-Jdk": "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            "Built-By": System.properties['user.name'],
            "Built-On": new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            "Implementation-Build": "git rev-parse --verify HEAD".execute().getText().trim(),
            "Implementation-Title": "OMERO Zarr Pixel Buffer",
            "Implementation-Version": archiveVersion,
            "Implementation-Vendor": "Glencoe Software Inc.",
        )
    }
}

checkstyle {
    toolVersion = "10.26.1"
}

// Task to copy all runtime dependencies to libs directory
tasks.register('copyDependencies', Copy) {
    from configurations.runtimeClasspath
    into 'libs'
}
