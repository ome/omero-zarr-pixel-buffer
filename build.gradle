plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'checkstyle'
    id 'com.autonomousapps.dependency-analysis' version '3.5.1'
}

group = 'com.glencoesoftware.omero'
version = '0.6.2-SNAPSHOT'

ext {
    awsSdkVersion = '2.34.6'
    jacksonVersion = '2.20.0'
    zarrJavaVersion = '0.0.10'
    formatsVersion = '8.3.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
    maven {
        name = 'Unidata'
        url = 'https://artifacts.unidata.ucar.edu/repository/unidata-releases/'
    }
    maven {
        url = 'https://artifacts.glencoesoftware.com/artifactory/ome.releases'
    }
    maven {
        url = 'https://artifacts.glencoesoftware.com/artifactory/ome.external'
    }
    maven {
        url = 'https://artifacts.glencoesoftware.com/artifactory/scijava-thirdparty'
    }
    maven {
        url = 'https://repo.glencoesoftware.com/repository/bioformats2raw2ometiff/'
    }
}

dependencies {
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
    implementation "dev.zarr:zarr-java:${zarrJavaVersion}"

    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "ome:formats-api:${formatsVersion}"
    implementation "ome:formats-gpl:${formatsVersion}"
    implementation 'org.apache.tika:tika-core:1.28.5'
    implementation 'ch.qos.logback:logback-classic:1.3.14'

    implementation "software.amazon.awssdk:aws-core:${awsSdkVersion}"
    implementation "software.amazon.awssdk:sdk-core:${awsSdkVersion}"
    implementation "software.amazon.awssdk:retries:${awsSdkVersion}"
    implementation "software.amazon.awssdk:aws-sdk-java:${awsSdkVersion}"
    //implementation "software.amazon.awssdk:apache-client:${awsSdkVersion}" Might not be needed

    implementation 'edu.ucar:cdm-core:5.9.1'
    implementation 'org.perf4j:perf4j:0.9.16'
    implementation 'org.slf4j:slf4j-api:2.0.9'

    implementation 'org.openmicroscopy:omero-blitz:5.8.3'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.apache.commons:commons-compress:1.26.0'
}

test {
  useJUnit()
}

publishing {
    publications {
        omeroZarrPixelBuffer(MavenPublication) {
            from components.java
            pom {
                name = 'OMERO Zarr Pixel Buffer'
                licenses {
                    license {
                        name = 'GNU General Public License version 2'
                        url = 'http://www.gnu.org/licenses/gpl-2.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = 'https://artifacts.glencoesoftware.com/artifactory/gs-omero-snapshots-local/'
            credentials {
               username = project.properties.ArtifactoryUserName
               password = project.properties.ArtifactoryPassword
            }
        }
    }
}

jar {
    manifest {
        attributes(
            "Created-By": "Gradle ${gradle.gradleVersion}",
            "Build-Jdk": "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            "Built-By": System.properties['user.name'],
            "Built-On": new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            "Implementation-Build": "git rev-parse --verify HEAD".execute().getText().trim(),
            "Implementation-Title": "OMERO Zarr Pixel Buffer",
            "Implementation-Version": archiveVersion,
            "Implementation-Vendor": "Glencoe Software Inc.",
        )
    }
}

checkstyle {
    toolVersion = "10.26.1"
}

dependencyAnalysis {
  issues {
    all {
      onAny {
        severity("fail")
      }
    }
  }
  reporting {
    printBuildHealth(true)
  }
}

tasks.withType(JavaCompile) {
    // get rid of the "-Xlint:deprecation for details" message
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

tasks.register('copyRuntimeDeps', Copy) {
    from configurations.runtimeClasspath
    into layout.buildDirectory.dir('runtime-libs')
}
